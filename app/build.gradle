plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id "androidx.navigation.safeargs.kotlin"
    id 'com.mikepenz.aboutlibraries.plugin'
}

def appName = "WebDAV Provider"
def appNameDebug = "${appName} (Debug)"
def packageName = "dev.rocli.android.webdav"
def providerAuthority = "${packageName}.provider"
def providerAuthorityDebug = "${packageName}.debug.provider"

def getCmdOutput = { cmd ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine cmd
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitHash = { -> return getCmdOutput(["git", "rev-parse", "--short", "HEAD"]) }
def getGitBranch = { -> return getCmdOutput(["git", "rev-parse", "--abbrev-ref", "HEAD"]) }

/**
 * 30-bit Version Code Schema (7-7-7-9 bits)
 * 
 * | Component | Bits | Range   | Shift Position | Description                        |
 * |-----------|------|---------|----------------|------------------------------------|
 * | Major     | 7    | 0-127   | << 23          | Major version                      |
 * | Minor     | 7    | 0-127   | << 16          | Minor version                      |
 * | Patch     | 7    | 0-127   | << 9           | Patch version                      |
 * | Qualifier | 9    | 0-511   | << 0           | 511 for stable, 0-510 for beta     |
 * 
 * Stable releases use qualifier=511 to ensure they always rank higher than beta builds.
 * Beta builds use commits-since-last-tag as the qualifier (capped at 510).
 */
def calculateVersionCode = { int major, int minor, int patch, boolean isStable, int buildCount ->
    def MAX_QUALIFIER = 511
    
    if (major > 127 || minor > 127 || patch > 127) {
        throw new GradleException("Version components exceed allocated bit width (max 127)")
    }
    if (!isStable && buildCount >= MAX_QUALIFIER) {
        throw new GradleException("Build count ${buildCount} exceeds limit of ${MAX_QUALIFIER - 1} for beta builds")
    }
    
    def qualifier = isStable ? MAX_QUALIFIER : buildCount
    return (major << 23) | (minor << 16) | (patch << 9) | qualifier
}

// Get version from environment or git tags
def getVersionFromGit = { ->
    try {
        def tag = getCmdOutput(["git", "describe", "--tags", "--abbrev=0", "--match", "v*.*.*"])
        // Parse version from tag (e.g., "v1.2.3" -> [1, 2, 3])
        def matcher = tag =~ /^v(\d+)\.(\d+)\.(\d+)$/
        if (matcher.matches()) {
            return [
                major: matcher[0][1] as int,
                minor: matcher[0][2] as int,
                patch: matcher[0][3] as int,
                tag: tag
            ]
        }
    } catch (Exception ignored) {
        // No matching tag found
    }
    // Default fallback
    return [major: 0, minor: 0, patch: 0, tag: "v0.0.0"]
}

def getCommitsSinceTag = { ->
    try {
        def tag = getCmdOutput(["git", "describe", "--tags", "--abbrev=0", "--match", "v*.*.*"])
        return getCmdOutput(["git", "rev-list", "--count", "HEAD", "^${tag}"]) as int
    } catch (Exception ignored) {
        return 0
    }
}

// Version configuration from environment variables or git
def envMajor = System.getenv("VERSION_MAJOR")
def envMinor = System.getenv("VERSION_MINOR")
def envPatch = System.getenv("VERSION_PATCH")
def envIsStable = System.getenv("VERSION_STABLE") == "true"
def envBuildCount = System.getenv("VERSION_BUILD_COUNT")

def gitVersion = getVersionFromGit()
def vMajor = envMajor != null ? envMajor as int : gitVersion.major
def vMinor = envMinor != null ? envMinor as int : gitVersion.minor
def vPatch = envPatch != null ? envPatch as int : gitVersion.patch
def isStable = envIsStable
def buildCount = envBuildCount != null ? envBuildCount as int : getCommitsSinceTag()

def computedVersionCode = calculateVersionCode(vMajor, vMinor, vPatch, isStable, buildCount)
def computedVersionName = isStable ? "v${vMajor}.${vMinor}.${vPatch}" : "v${vMajor}.${vMinor}.${vPatch}-beta.${buildCount}"

android {
    namespace packageName

    compileSdk 35

    defaultConfig {
        applicationId packageName
        minSdkVersion 27
        targetSdkVersion 35
        versionCode computedVersionCode
        versionName computedVersionName

        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        buildConfigField "String", "GIT_BRANCH", "\"${getGitBranch()}\""
        buildConfigField "java.util.concurrent.atomic.AtomicBoolean", "TEST", "new java.util.concurrent.atomic.AtomicBoolean(false)"
        buildConfigField "java.util.concurrent.atomic.AtomicBoolean", "DYNAMIC_COLORS", "new java.util.concurrent.atomic.AtomicBoolean(true)"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }

        testInstrumentationRunner "dev.rocli.android.webdav.WebDavTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'

        unitTests {
            all {
                ignoreFailures false
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"

                    showExceptions true
                    exceptionFormat "full"
                    showCauses true
                    showStackTraces true
                }
            }

            includeAndroidResources true
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        buildConfig true
    }

    signingConfigs {
        if (System.getenv("ANDROID_KEYSTORE") != null) {
            create("multiset") {
                storeFile file(System.getenv("ANDROID_KEYSTORE"))
                storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
                keyAlias System.getenv("ANDROID_KEY_ALIAS")
                keyPassword System.getenv("ANDROID_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            resValue("string", "app_name", appNameDebug)
            manifestPlaceholders = [providerAuthority: providerAuthorityDebug]
            buildConfigField("String", "PROVIDER_AUTHORITY", "\"${providerAuthorityDebug}\"")
        }
        release {
            resValue("string", "app_name", appName)
            manifestPlaceholders = [providerAuthority: providerAuthority]
            buildConfigField("String", "PROVIDER_AUTHORITY", "\"${providerAuthority}\"")

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.findByName("multiset")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    lint {
        abortOnError true
        checkDependencies true
    }

    aboutLibraries {
        // Tasks for aboutLibraries are not run automatically to keep the build reproducible
        // To update manually: ./gradlew app:exportLibraryDefinitions -PaboutLibraries.exportPath=src/main/res/raw
        prettyPrint = true
        fetchRemoteFunding = false
        registerAndroidTasks = false
        exclusionPatterns = [~"javax.annotation.*"]
        duplicationMode = com.mikepenz.aboutlibraries.plugin.DuplicateMode.MERGE
    }
}

dependencies {
    def roomVersion = "2.6.1"
    def espressoVersion = '3.5.1'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.8.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.3'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation 'com.google.android.material:material:1.12.0'
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    implementation "com.mikepenz:aboutlibraries:$aboutLibrariesVersion"
    implementation "com.mikepenz:aboutlibraries-core:$aboutLibrariesVersion"
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-simplexml:2.11.0'
    implementation 'com.github.thegrizzlylabs:sardine-android:0.9'
    implementation 'io.github.rburgst:okhttp-digest:3.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'
    kapt "androidx.room:room-compiler:$roomVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"

    // NOTE: Don't update test/espresso libraries until this is resolved: https://github.com/android/android-test/issues/2273
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.3.0"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    androidTestUtil 'androidx.test:orchestrator:1.4.2'
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hiltVersion"
}
